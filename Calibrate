#include Box
#include ExtraTools/OCR/GDIp.ahk
#include Settings

global tmp_loc := RuneEscapeAppData . "tmp.png"

grab_point() {
	CoordMode Mouse, Screen

	Loop {
		MouseGetPos x, y
		ToolTip Calibrating (Right Click), (x + 20), (y + 20), 1

		if(GetKeyState("RButton", "P")) {
			MouseGetPos x, y
			return [x, y]
		}

		Sleep 10	; So as not to peg CPU
	}
}

grab_rectangle() {
	Box_Init()
	CoordMode Mouse, Screen
	isDrawing := 0
	downXLoc := 0
	downYLoc := 0

	Loop {
		MouseGetPos x, y
		ToolTip Calibrating (Hold Shift), (x + 20), (y + 20), 1

		if(isDrawing) {
			Box_Draw(downXLoc, downYLoc, x-downXLoc, y-downYLoc)
		}

		if(GetKeyState("LShift", "P")) {
			if(!isDrawing) {
				MouseGetPos x, y
				downXLoc := x
				downYLoc := y
			}

			isDrawing := 1
		}
		else {
			if(isDrawing) {
				MouseGetPos x, y
				Box_Destroy()
				ToolTip
				return [downXLoc, downYLoc, x - downXLoc, y - downYLoc]
			}

			isDrawing := 0
			Box_Hide()
		}

		if(GetKeyState("Esc", "P")) {
			MsgBox No longer trying to calibrate rectangle
			Box_Destroy()
			ToolTip
			return [0, 0, 0, 0]
		}

		Sleep 10	; So as not to peg CPU
	}
}

calibrate(item) {
	Box_Init()
	CoordMode Mouse, Screen
	isDrawing := 0
	downXLoc := 0
	downYLoc := 0

	MsgBox Oh No! A required resource is not yet calibrated!`n`nDraw a rectangle to calibrate %item%`n`nPress Esc to cancel

	rectangle := grab_rectangle()

	success := ableToProcessRectangle(rectangle[1], rectangle[2], rectangle[3], rectangle[4])
	if(!success) {
		FileDelete %tmp_loc%
		calibrate(item)
	}
	else {
		new_loc := RuneEscapeAppData . item . ".png"
		FileMove %tmp_loc%, %new_loc%
		return
	}
}

ableToProcessRectangle(x, y, w, h) {
	Box_Hide()

	; [TODO] Make it so reverse scans will work, currently they only render black

	pToken:=Gdip_Startup()
	pBitmap:=Gdip_BitmapFromScreen(x "|" y "|" w "|" h)
	Gdip_SaveBitmapToFile(pBitmap, tmp_loc)
	Gdip_Shutdown(pToken)

	Gui, Add, Picture,, %tmp_loc%
	Gui, Show,, Calibration Window

	MsgBox 4,, Are you happy with this calibration?
	Gui Destroy
	IfMsgBox Yes
	{
		return 1
	}
	else {
		return 0
	}
}